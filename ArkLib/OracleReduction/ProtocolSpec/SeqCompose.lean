/-
Copyright (c) 2024-2025 ArkLib Contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Quang Dao
-/

import ArkLib.Data.Fin.Sigma
import ArkLib.OracleReduction.ProtocolSpec.Cast

/-! # Sequential Composition of Protocol Specifications

This file collects all definitions and theorems about sequentially composing `ProtocolSpec`s and
their associated data. -/

universe u v

open OracleComp OracleSpec

namespace ProtocolSpec

/-! ### Composition of Two Protocol Specifications -/

variable {m n : ℕ}

/-- Adding a round with direction `dir` and type `Message` to the beginning of a `ProtocolSpec` -/
abbrev cons (pSpec : ProtocolSpec n) (dir : Direction) (Message : Type) :
    ProtocolSpec (n + 1) :=
  ⟨Fin.vcons dir pSpec.dir, Fin.vcons Message pSpec.Type⟩

/-- Concatenate a round with direction `dir` and type `Message` to the end of a `ProtocolSpec` -/
abbrev concat (pSpec : ProtocolSpec n) (dir : Direction) (Message : Type) :
    ProtocolSpec (n + 1) :=
  ⟨Fin.vconcat pSpec.dir dir, Fin.vconcat pSpec.Type Message⟩

/-- Appending two `ProtocolSpec`s -/
abbrev append (pSpec : ProtocolSpec m) (pSpec' : ProtocolSpec n) : ProtocolSpec (m + n) :=
  ⟨Fin.vappend pSpec.dir pSpec'.dir, Fin.vappend pSpec.Type pSpec'.Type⟩

@[inherit_doc]
infixl : 65 " ++ₚ " => ProtocolSpec.append

@[simp]
theorem append_cast_left {n m : ℕ} {pSpec : ProtocolSpec n} {pSpec' : ProtocolSpec m} (n' : ℕ)
    (h : n + m = n' + m) :
      dcast h (pSpec ++ₚ pSpec') = (dcast (Nat.add_right_cancel h) pSpec) ++ₚ pSpec' := by
  simp only [append, dcast, ProtocolSpec.cast, Fin.vappend_eq_append]
  simp

@[simp]
theorem append_cast_right {n m : ℕ} (pSpec : ProtocolSpec n) (pSpec' : ProtocolSpec m) (m' : ℕ)
    (h : n + m = n + m') :
      dcast h (pSpec ++ₚ pSpec') = pSpec ++ₚ (dcast (Nat.add_left_cancel h) pSpec') := by
  simp only [append, dcast, ProtocolSpec.cast, Fin.vappend_eq_append, Fin.append_cast_right]

theorem append_left_injective {pSpec : ProtocolSpec n} :
    Function.Injective (@ProtocolSpec.append m n · pSpec) := by
  simp only [append, Fin.vappend_eq_append]
  intro x y h
  simp at h
  obtain ⟨hDir, hType⟩ := h
  ext i
  · simp [Fin.append_left_injective pSpec.dir hDir]
  · simp [Fin.append_left_injective pSpec.Type hType]

theorem append_right_injective {pSpec : ProtocolSpec m} :
    Function.Injective (@ProtocolSpec.append m n pSpec) := by
  unfold ProtocolSpec.append
  simp only [Fin.vappend_eq_append]
  intro x y h
  simp at h
  obtain ⟨hDir, hType⟩ := h
  ext i
  · simp [Fin.append_right_injective pSpec.dir hDir]
  · simp [Fin.append_right_injective pSpec.Type hType]

@[simp]
theorem append_left_cancel_iff {pSpec : ProtocolSpec n} {p1 p2 : ProtocolSpec m} :
    p1 ++ₚ pSpec = p2 ++ₚ pSpec ↔ p1 = p2 :=
  ⟨fun h => append_left_injective h, fun h => by rw [h]⟩

@[simp]
theorem append_right_cancel_iff {pSpec : ProtocolSpec m} {p1 p2 : ProtocolSpec n} :
    pSpec ++ₚ p1 = pSpec ++ₚ p2 ↔ p1 = p2 :=
  ⟨fun h => append_right_injective h, fun h => by rw [h]⟩

@[simp]
theorem snoc_take {pSpec : ProtocolSpec n} (k : ℕ) (h : k < n) :
    (pSpec.take k (Nat.le_of_succ_le h) ++ₚ ⟨![pSpec.dir ⟨k, h⟩], ![pSpec.Type ⟨k, h⟩]⟩)
      = pSpec.take (k + 1) h := by
  simp only [append, take, Fin.vappend_eq_append, Fin.append_right_eq_snoc,
    Fin.take_succ_eq_snoc]
  ext : 1 <;> simp

variable {pSpec₁ : ProtocolSpec m} {pSpec₂ : ProtocolSpec n}

@[simp]
theorem take_append_left :
    (pSpec₁ ++ₚ pSpec₂).take m (Nat.le_add_right m n) = pSpec₁ := by
  simp only [take, Fin.vappend_eq_append]
  ext <;> simp [Fin.take_apply]

@[simp]
theorem take_append_left' : (pSpec₁ ++ₚ pSpec₂)⟦:m⟧ = pSpec₁ :=
  take_append_left

/-- `take_append_left` at a general cut `k ≤ m`, not only at `k = m`. This is the form a round
induction over an appended protocol needs, since it cuts at every intermediate round. -/
theorem take_append_left_of_le {k : ℕ} (hk : k ≤ m) :
    (pSpec₁ ++ₚ pSpec₂).take k (by omega) = pSpec₁.take k hk := by
  have key : ∀ (i : Fin k), (Fin.castLE (by omega : k ≤ m + n) i)
      = Fin.castAdd n (Fin.castLE hk i) := fun i => Fin.ext rfl
  simp only [take, Fin.vappend_eq_append]
  ext i
  · simp only [Fin.take_apply, key, Fin.append_left]
  · simp only [Fin.take_apply, key, Fin.append_left]

/-- The direction of an appended protocol at a left-injected round is the left component's. -/
@[simp]
theorem dir_append_castAdd (i : Fin m) :
    (pSpec₁ ++ₚ pSpec₂).dir (Fin.castAdd n i) = pSpec₁.dir i := by
  simp [ProtocolSpec.append, Fin.vappend_eq_append, Fin.append_left]

/-- `dir_append_castAdd` at a raw `Fin.mk` index. Stated separately, and proved by handing the
`castAdd` form straight back, because rewriting `⟨v, _⟩` into `Fin.castAdd n ⟨v, _⟩` inside a goal
fails on a non-type-correct motive once a bound like `v < m` is in play: the bound mentions the very
index being rewritten. Taking the value and its two bounds as parameters makes the two sides
definitionally equal, so no rewrite is needed. -/
theorem dir_append_lt (v : ℕ) (hv : v < m) (h : v < m + n) :
    (pSpec₁ ++ₚ pSpec₂).dir ⟨v, h⟩ = pSpec₁.dir ⟨v, hv⟩ :=
  dir_append_castAdd (pSpec₂ := pSpec₂) ⟨v, hv⟩

/-- The transcript type of an appended protocol, cut at a round inside the left component, is the
left component's transcript type. The transcript-level counterpart of `prvState_castSucc_inl`, and
the transport a round induction carries alongside the state. -/
theorem transcript_append_castAdd (i : Fin (m + 1)) :
    (pSpec₁ ++ₚ pSpec₂).Transcript (Fin.cast (by omega) (Fin.castAdd n i))
      = pSpec₁.Transcript i := by
  change ((pSpec₁ ++ₚ pSpec₂).take _ _).FullTranscript = (pSpec₁.take _ _).FullTranscript
  simp only [Fin.val_cast, Fin.val_castAdd]
  rw [take_append_left_of_le (show (i : ℕ) ≤ m from i.is_le)]

@[simp]
theorem rtake_append_right :
    (pSpec₁ ++ₚ pSpec₂).rtake n (Nat.le_add_left n m) = pSpec₂ := by
  simp only [rtake, Fin.vappend_eq_append]
  ext i : 2 <;> simp [Fin.rtake, Fin.append_right]

/-- Left type transport for `++ₚ` at a `castAdd` index: the appended spec's type
at an index in the first half is the left spec's type. -/
theorem append_Type_castAdd (i : Fin m) :
    (pSpec₁ ++ₚ pSpec₂).«Type» (Fin.castAdd n i) = pSpec₁.«Type» i := by
  simp only [Fin.vappend_eq_append, Fin.append_left]

/-- Right type transport for `++ₚ` at a `natAdd` index: the appended spec's type
at an index in the second half is the right spec's type. -/
theorem append_Type_natAdd (i : Fin n) :
    (pSpec₁ ++ₚ pSpec₂).«Type» (Fin.natAdd m i) = pSpec₂.«Type» i := by
  simp only [Fin.vappend_eq_append, Fin.append_right]

/-- `append_Type_castAdd` at a raw `Fin.mk` index. See `dir_append_lt` for why the value-and-bounds
form is the usable one. -/
theorem type_append_lt (v : ℕ) (hv : v < m) (h : v < m + n) :
    (pSpec₁ ++ₚ pSpec₂).Type ⟨v, h⟩ = pSpec₁.Type ⟨v, hv⟩ :=
  append_Type_castAdd (pSpec₂ := pSpec₂) ⟨v, hv⟩

/-- The direction of an appended protocol at a right-injected round is the right component's. -/
theorem dir_append_natAdd (i : Fin n) :
    (pSpec₁ ++ₚ pSpec₂).dir (Fin.natAdd m i) = pSpec₂.dir i := by
  simp [ProtocolSpec.append, Fin.vappend_eq_append, Fin.append_right]

/-- `type_append_lt`'s counterpart at or after the boundary. Unlike the left-hand versions this one
cannot hand the `natAdd` form straight back: `Fin.natAdd m ⟨v - m, _⟩` has value `m + (v - m)`,
which is `v` only under `m ≤ v` and so not definitionally, hence the explicit index rewrite. -/
theorem type_append_ge (v : ℕ) (hm : m ≤ v) (h : v < m + n) :
    (pSpec₁ ++ₚ pSpec₂).Type ⟨v, h⟩ = pSpec₂.Type ⟨v - m, by omega⟩ := by
  have hidx : (⟨v, h⟩ : Fin (m + n)) = Fin.natAdd m ⟨v - m, by omega⟩ :=
    Fin.ext (by simp only [Fin.val_natAdd]; omega)
  rw [hidx]
  exact append_Type_natAdd _

/-- `dir_append_lt`'s counterpart at or after the boundary. -/
theorem dir_append_ge (v : ℕ) (hm : m ≤ v) (h : v < m + n) :
    (pSpec₁ ++ₚ pSpec₂).dir ⟨v, h⟩ = pSpec₂.dir ⟨v - m, by omega⟩ := by
  have hidx : (⟨v, h⟩ : Fin (m + n)) = Fin.natAdd m ⟨v - m, by omega⟩ :=
    Fin.ext (by simp only [Fin.val_natAdd]; omega)
  rw [hidx]
  exact dir_append_natAdd _

/-- `type_append_ge` with the right-region round written as `m + w` rather than as `v` with `v - m`.
`Fin.natAdd m ⟨w, _⟩` *is* `⟨m + w, _⟩` definitionally, so unlike `type_append_ge` this needs no
index rewrite -- which is what makes `m + w` the right way to index a right-region induction. -/
theorem type_append_add (w : ℕ) (hw : w < n) (h : m + w < m + n) :
    (pSpec₁ ++ₚ pSpec₂).Type ⟨m + w, h⟩ = pSpec₂.Type ⟨w, hw⟩ :=
  append_Type_natAdd (pSpec₁ := pSpec₁) ⟨w, hw⟩

/-- `dir_append_ge` in the `m + w` indexing. See `type_append_add`. -/
theorem dir_append_add (w : ℕ) (hw : w < n) (h : m + w < m + n) :
    (pSpec₁ ++ₚ pSpec₂).dir ⟨m + w, h⟩ = pSpec₂.dir ⟨w, hw⟩ :=
  dir_append_natAdd (pSpec₁ := pSpec₁) ⟨w, hw⟩

/-- Transport a left-component transcript into the appended protocol, **pointwise**.

Deliberately not a `cast` of the whole function type: a round induction extends the transcript with
`Fin.snoc` at every step, and `Fin.snoc` commutes with a pointwise transport computably
(`liftTranscript_snoc`) whereas moving it across a `cast` of a Pi type does not reduce. -/
def liftTranscript (v : ℕ) (hv : v ≤ m) (hvn : v ≤ m + n)
    (T : pSpec₁.Transcript ⟨v, by omega⟩) :
    (pSpec₁ ++ₚ pSpec₂).Transcript ⟨v, by omega⟩ :=
  fun i => cast (type_append_lt (pSpec₁ := pSpec₁) (pSpec₂ := pSpec₂)
    (i : ℕ) (Nat.lt_of_lt_of_le i.isLt hv) (Nat.lt_of_lt_of_le i.isLt hvn)).symm (T i)

/-- Extending a transcript and transporting it commute. This is the transcript half of a round
induction's step; the state half is `Prover.prvState_lt'`. -/
theorem liftTranscript_snoc (v : ℕ) (hv : v < m) (h : v < m + n)
    (T : pSpec₁.Transcript ⟨v, by omega⟩) (msg : pSpec₁.Type ⟨v, hv⟩) :
    liftTranscript (pSpec₂ := pSpec₂) (v + 1) (by omega) (by omega) (Fin.snoc T msg)
      = Fin.snoc (liftTranscript (pSpec₂ := pSpec₂) v (by omega) (by omega) T)
                 (cast (type_append_lt (pSpec₂ := pSpec₂) v hv h).symm msg) := by
  funext i
  refine Fin.lastCases ?_ ?_ i
  · simp [liftTranscript]
  · intro i'
    simp [liftTranscript]
    rfl

/-- `liftTranscript_snoc` phrased with `Transcript.concat`. The two are definitionally equal, but
`rw` matches syntactically, and a round induction's goals carry `Transcript.concat`. -/
theorem liftTranscript_concat (v : ℕ) (hv : v < m) (h : v < m + n)
    (T : pSpec₁.Transcript ⟨v, by omega⟩) (msg : pSpec₁.Type ⟨v, hv⟩) :
    liftTranscript (pSpec₂ := pSpec₂) (v + 1) (by omega) (by omega) (Transcript.concat msg T)
      = Transcript.concat (cast (type_append_lt (pSpec₂ := pSpec₂) v hv h).symm msg)
          (liftTranscript (pSpec₂ := pSpec₂) v (by omega) (by omega) T) :=
  liftTranscript_snoc v hv h T msg

/-- Combine a full `pSpec₁` transcript with a partial `pSpec₂` transcript into a partial transcript
of the appended protocol, pointwise. The right-region counterpart of `liftTranscript`: once a round
index passes the boundary the transcript is `pSpec₁`'s entirely plus however much of `pSpec₂` has
been produced.

Indexed by `m + w`, not by `v` with `v - m`: `m + (w + 1)` is definitionally `(m + w) + 1`, so a
round induction extending this by one never has to normalise its own index. -/
def liftTranscriptR (w : ℕ) (hw : w ≤ n)
    (T₁ : pSpec₁.FullTranscript) (T₂ : pSpec₂.Transcript ⟨w, by omega⟩) :
    (pSpec₁ ++ₚ pSpec₂).Transcript ⟨m + w, by omega⟩ :=
  fun i =>
    have hi : (i : ℕ) < m + w := i.isLt
    if h : (i : ℕ) < m
      then cast (type_append_lt (pSpec₁ := pSpec₁) (pSpec₂ := pSpec₂) (i : ℕ) h
        (by omega)).symm (T₁ ⟨i, h⟩)
      else cast (type_append_ge (pSpec₁ := pSpec₁) (pSpec₂ := pSpec₂) (i : ℕ) (by omega)
        (by omega)).symm (T₂ ⟨(i : ℕ) - m, by show (i : ℕ) - m < w; omega⟩)

set_option maxHeartbeats 2000000 in
/-- Extending a right-region transcript and combining it commute -- the right-region counterpart of
`liftTranscript_concat`. The proof is a case split at every level: outer index below or above the
boundary, and `Fin.snoc` at `castSucc` or at `last`. The mixed cases are impossible and die by
`omega`, but only once `Fin.val_castLT` has normalised the index so the contradiction is visible. -/
theorem liftTranscriptR_concat (w : ℕ) (hw : w < n)
    (T₁ : pSpec₁.FullTranscript) (T₂ : pSpec₂.Transcript ⟨w, by omega⟩)
    (msg : pSpec₂.Type ⟨w, hw⟩) :
    liftTranscriptR (pSpec₁ := pSpec₁) (w + 1) (by omega) T₁ (Transcript.concat msg T₂)
      = Transcript.concat
          (cast (type_append_add (pSpec₁ := pSpec₁) w hw (by omega)).symm msg)
          (liftTranscriptR (pSpec₁ := pSpec₁) w (by omega) T₁ T₂) := by
  funext i
  refine Fin.lastCases ?_ ?_ i
  · simp only [liftTranscriptR, Transcript.concat, Fin.val_last, Nat.add_eq, Fin.snoc]
    split
    · omega
    · split
      · omega
      · split
        · omega
        · exact eq_of_heq (((cast_heq _ _).trans (cast_heq _ _)).trans
            ((cast_heq _ _).trans (cast_heq _ _)).symm)
  · intro i'
    have hi' : (i' : ℕ) < m + w := i'.isLt
    simp only [liftTranscriptR, Transcript.concat, Fin.coe_castSucc, Nat.add_eq, Fin.snoc,
      Fin.val_castLT, Fin.val_castSucc]
    split <;> (try split) <;> (try split) <;>
      first
        | omega
        | (simp only [cast_cast]; rfl)
        | exact eq_of_heq ((cast_heq _ _).trans
            ((cast_heq _ _).trans (cast_heq _ _)).symm)
        | exact eq_of_heq (((cast_heq _ _).trans (cast_heq _ _)).trans
            (cast_heq _ _).symm)
        | exact eq_of_heq (((cast_heq _ _).trans (cast_heq _ _)).trans
            ((cast_heq _ _).trans (cast_heq _ _)).symm)
        | exact eq_of_heq ((cast_heq _ _).trans (cast_heq _ _).symm)

namespace Transcript

variable {k : Fin (m + n + 1)}

/-- The first half of a partial transcript for a concatenated protocol, up to round `k < m + n + 1`.

This is defined to be the full transcript for the first half if `k ≥ m`. -/
def fst (T : (pSpec₁ ++ₚ pSpec₂).Transcript k) : pSpec₁.Transcript ⟨min k m, by omega⟩ :=
  fun i => by
    have him : (i : ℕ) < min (k : ℕ) m := i.isLt
    exact _root_.cast
      (append_Type_castAdd (pSpec₁ := pSpec₁) (pSpec₂ := pSpec₂) ⟨i.val, by omega⟩)
      (T ⟨i.val, by omega⟩)

/-- The second half of a partial transcript for a concatenated protocol. -/
def snd (T : (pSpec₁ ++ₚ pSpec₂).Transcript k) : pSpec₂.Transcript ⟨k - m, by omega⟩ :=
  fun i => by
    have him : (i : ℕ) < (k : ℕ) - m := i.isLt
    have hk := k.isLt
    exact _root_.cast
      (append_Type_natAdd (pSpec₁ := pSpec₁) (pSpec₂ := pSpec₂) ⟨i.val, by omega⟩)
      (T ⟨m + i.val, by omega⟩)

end Transcript

namespace FullTranscript

/-- Appending two transcripts for two `ProtocolSpec`s -/
def append (T₁ : FullTranscript pSpec₁) (T₂ : FullTranscript pSpec₂) :
    FullTranscript (pSpec₁ ++ₚ pSpec₂) :=
  Fin.happend T₁ T₂

@[inherit_doc]
infixl : 65 " ++ₜ " => append

/-- Adding a message with a given direction and type to the end of a `Transcript` -/
def concat {pSpec : ProtocolSpec n} {NextMessage : Type}
    (T : FullTranscript pSpec) (dir : Direction) (msg : NextMessage) :
        FullTranscript (pSpec ++ₚ ⟨!v[dir], !v[NextMessage]⟩) :=
  Fin.hconcat T msg

-- TODO: fill

-- @[simp]
-- theorem append_cast_left {n m : ℕ} {pSpec₁ pSpec₂ : ProtocolSpec n} {pSpec' : ProtocolSpec m}
--     {T₁ : FullTranscript pSpec₁} {T₂ : FullTranscript pSpec'} (n' : ℕ)
--     (h : n + m = n' + m) (hSpec : dcast h pSpec₁ = pSpec₂) :
--       dcast₂ h (by simp) (T₁ ++ₜ T₂) = (dcast₂ (Nat.add_right_cancel h) (by simp) T₁) ++ₜ T₂ :=
-- by
--   simp [append, dcast₂, ProtocolSpec.cast, Fin.append_cast_left]

-- @[simp]
-- theorem append_cast_right {n m : ℕ} (pSpec : ProtocolSpec n) (pSpec' : ProtocolSpec m) (m' : ℕ)
--     (h : n + m = n + m') :
--       dcast h (pSpec ++ₚ pSpec') = pSpec ++ₚ (dcast (Nat.add_left_cancel h) pSpec') := by
--   simp [append, dcast, ProtocolSpec.cast, Fin.append_cast_right]

@[simp]
theorem take_append_left (T : FullTranscript pSpec₁) (T' : FullTranscript pSpec₂) :
    (T ++ₜ T').take m (Nat.le_add_right m n) =
      T.cast rfl (by simp [ProtocolSpec.append]) := by
  ext i
  simp [take, append, ProtocolSpec.append, Fin.castLE,
    FullTranscript.cast, Transcript.cast]
  have : ⟨i.val, by omega⟩ = Fin.castAdd n i := by ext; simp
  rw! (castMode := .all) [this, Fin.happend_left]
  rfl

@[simp]
theorem rtake_append_right (T : FullTranscript pSpec₁) (T' : FullTranscript pSpec₂) :
    (T ++ₜ T').rtake n (Nat.le_add_left n m) =
      T'.cast rfl (by simp [ProtocolSpec.append]) := by
  ext i
  simp [rtake, Fin.rtake, append, Fin.cast, FullTranscript.cast, Transcript.cast]
  have : ⟨m + n - n + i.val, by omega⟩ = Fin.natAdd m i := by ext; simp
  rw! (castMode := .all) [this, Fin.happend_right]
  apply eq_of_heq
  exact ((eqRec_heq _ _).trans (cast_heq _ _)).trans (cast_heq _ _).symm

/-- The first half of a transcript for a concatenated protocol -/
def fst (T : FullTranscript (pSpec₁ ++ₚ pSpec₂)) : FullTranscript pSpec₁ :=
  fun i => by
    simpa [ProtocolSpec.append, Fin.vappend_eq_append, Fin.append_left]
      using T (Fin.castAdd n i)

/-- The second half of a transcript for a concatenated protocol -/
def snd (T : FullTranscript (pSpec₁ ++ₚ pSpec₂)) : FullTranscript pSpec₂ :=
  fun i => by
    simpa [ProtocolSpec.append, Fin.vappend_eq_append, Fin.append_right]
      using T (Fin.natAdd m i)

@[simp]
theorem append_fst (T₁ : FullTranscript pSpec₁) (T₂ : FullTranscript pSpec₂) :
    (T₁ ++ₜ T₂).fst = T₁ := by
  funext i
  simp [fst, append]

@[simp]
theorem append_snd (T₁ : FullTranscript pSpec₁) (T₂ : FullTranscript pSpec₂) :
    (T₁ ++ₜ T₂).snd = T₂ := by
  funext i
  simp [snd, append]

end FullTranscript

def MessageIdx.inl (i : MessageIdx pSpec₁) : MessageIdx (pSpec₁ ++ₚ pSpec₂) :=
  ⟨Fin.castAdd n i.1, by simpa only [Fin.vappend_eq_append, Fin.append_left] using i.2⟩

def MessageIdx.inr (i : MessageIdx pSpec₂) : MessageIdx (pSpec₁ ++ₚ pSpec₂) :=
  ⟨Fin.natAdd m i.1, by simpa only [Fin.vappend_eq_append, Fin.append_right] using i.2⟩

@[simps!]
def MessageIdx.sumEquiv :
    MessageIdx pSpec₁ ⊕ MessageIdx pSpec₂ ≃ MessageIdx (pSpec₁ ++ₚ pSpec₂) where
  toFun := Sum.elim (MessageIdx.inl) (MessageIdx.inr)
  invFun := fun ⟨i, h⟩ => by
    by_cases hi : i < m
    · simp [Fin.vappend_eq_append, Fin.append, Fin.addCases, hi] at h
      exact Sum.inl ⟨⟨i, hi⟩, h⟩
    · simp [Fin.vappend_eq_append, Fin.append, Fin.addCases, hi] at h
      exact Sum.inr ⟨⟨i - m, by omega⟩, h⟩
  left_inv := fun i => by
    rcases i with ⟨⟨i, isLt⟩, h⟩ | ⟨⟨i, isLt⟩, h⟩ <;>
    simp [MessageIdx.inl, MessageIdx.inr, isLt]
  right_inv := fun ⟨i, h⟩ => by
    by_cases hi : i < m <;>
    simp [MessageIdx.inl, MessageIdx.inr, hi]
    congr; omega

def ChallengeIdx.inl (i : ChallengeIdx pSpec₁) : ChallengeIdx (pSpec₁ ++ₚ pSpec₂) :=
  ⟨Fin.castAdd n i.1, by simpa only [Fin.vappend_eq_append, Fin.append_left] using i.2⟩

def ChallengeIdx.inr (i : ChallengeIdx pSpec₂) : ChallengeIdx (pSpec₁ ++ₚ pSpec₂) :=
  ⟨Fin.natAdd m i.1, by simpa only [Fin.vappend_eq_append, Fin.append_right] using i.2⟩

/-- Restrict challenges for an appended protocol to its first component. -/
def Challenges.fst (challenges : (pSpec₁ ++ₚ pSpec₂).Challenges) : pSpec₁.Challenges :=
  fun i => by
    simpa [ChallengeIdx.inl, ProtocolSpec.append, Fin.vappend_eq_append,
      Fin.append_left] using challenges (ChallengeIdx.inl i)

/-- Restrict challenges for an appended protocol to its second component. -/
def Challenges.snd (challenges : (pSpec₁ ++ₚ pSpec₂).Challenges) : pSpec₂.Challenges :=
  fun i => by
    simpa [ChallengeIdx.inr, ProtocolSpec.append, Fin.vappend_eq_append,
      Fin.append_right] using challenges (ChallengeIdx.inr i)

/-- Restrict messages for an appended protocol to its first component. -/
def Messages.fst (messages : (pSpec₁ ++ₚ pSpec₂).Messages) : pSpec₁.Messages :=
  fun i => by
    simpa [MessageIdx.inl, ProtocolSpec.append, Fin.vappend_eq_append,
      Fin.append_left] using messages (MessageIdx.inl i)

/-- Restrict messages for an appended protocol to its second component. -/
def Messages.snd (messages : (pSpec₁ ++ₚ pSpec₂).Messages) : pSpec₂.Messages :=
  fun i => by
    simpa [MessageIdx.inr, ProtocolSpec.append, Fin.vappend_eq_append,
      Fin.append_right] using messages (MessageIdx.inr i)

@[simps!]
def ChallengeIdx.sumEquiv :
    ChallengeIdx pSpec₁ ⊕ ChallengeIdx pSpec₂ ≃ ChallengeIdx (pSpec₁ ++ₚ pSpec₂) where
  toFun := Sum.elim (ChallengeIdx.inl) (ChallengeIdx.inr)
  invFun := fun ⟨i, h⟩ => by
    by_cases hi : i < m
    · simp [Fin.vappend_eq_append, Fin.append, Fin.addCases, hi] at h
      exact Sum.inl ⟨⟨i, hi⟩, h⟩
    · simp [Fin.vappend_eq_append, Fin.append, Fin.addCases, hi] at h
      exact Sum.inr ⟨⟨i - m, by omega⟩, h⟩
  left_inv := fun i => by
    rcases i with ⟨⟨i, isLt⟩, h⟩ | ⟨⟨i, isLt⟩, h⟩ <;>
    simp [ChallengeIdx.inl, ChallengeIdx.inr, isLt]
  right_inv := fun ⟨i, h⟩ => by
    by_cases hi : i < m <;>
    simp [ChallengeIdx.inl, ChallengeIdx.inr, hi]
    congr; omega

/-- `sumEquiv.symm` maps a left-embedded composed challenge index back to `Sum.inl`. -/
@[simp]
theorem ChallengeIdx.sumEquiv_symm_inl (i₁ : ChallengeIdx pSpec₁) :
    (ChallengeIdx.sumEquiv (pSpec₁ := pSpec₁) (pSpec₂ := pSpec₂)).symm (ChallengeIdx.inl i₁)
      = Sum.inl i₁ := by
  rw [Equiv.symm_apply_eq]; simp

/-- `sumEquiv.symm` maps a right-embedded composed challenge index back to `Sum.inr`. -/
@[simp]
theorem ChallengeIdx.sumEquiv_symm_inr (i₂ : ChallengeIdx pSpec₂) :
    (ChallengeIdx.sumEquiv (pSpec₁ := pSpec₁) (pSpec₂ := pSpec₂)).symm (ChallengeIdx.inr i₂)
      = Sum.inr i₂ := by
  rw [Equiv.symm_apply_eq]; simp

/-- Sequential composition of a family of `ProtocolSpec`s, indexed by `i : Fin m`.

Defined for definitional equality, so that:
- `seqCompose !v[] = !p[]`
- `seqCompose !v[pSpec₁] = pSpec₁`
- `seqCompose !v[pSpec₁, pSpec₂] = pSpec₁ ++ₚ pSpec₂`
- `seqCompose !v[pSpec₁, pSpec₂, pSpec₃] = pSpec₁ ++ₚ (pSpec₂ ++ₚ pSpec₃)`
- and so on.

TODO: add notation `∑ i, pSpec i` for `seqCompose` -/
@[inline]
def seqCompose {m : ℕ} {n : Fin m → ℕ} (pSpec : ∀ i, ProtocolSpec (n i)) :
    ProtocolSpec (Fin.vsum n) where
  dir := Fin.vflatten (fun i => (pSpec i).dir)
  «Type» := Fin.vflatten (fun i => (pSpec i).Type)

@[simp]
lemma seqCompose_dir {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)} :
    (seqCompose pSpec).dir = Fin.vflatten (fun i => (pSpec i).dir) := by
  rfl

@[simp]
lemma seqCompose_type {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)} :
    (seqCompose pSpec).Type = Fin.vflatten (fun i => (pSpec i).Type) := by
  rfl

@[simp]
theorem seqCompose_zero {n : Fin 0 → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)} :
    seqCompose pSpec = !p[] := by
  rfl

@[simp]
theorem seqCompose_one {n : Fin 1 → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)} :
    seqCompose pSpec = pSpec 0 := by
  rfl

@[simp]
theorem seqCompose_two_eq_append {n : Fin 2 → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)} :
    seqCompose pSpec = append (pSpec 0) (pSpec 1) := by
  rfl

@[simp]
theorem seqCompose_succ_eq_append {m : ℕ} {n : Fin (m + 1) → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)} :
    seqCompose pSpec = append (pSpec 0) (seqCompose (fun i => pSpec (Fin.succ i))) := by
  rfl

@[simp]
theorem seqCompose_succ_dir {m : ℕ} {n : Fin (m + 1) → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)} :
    (seqCompose pSpec).dir = Fin.vflatten (fun i => (pSpec i).dir) := by
  rfl

@[simp]
theorem seqCompose_succ_type {m : ℕ} {n : Fin (m + 1) → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)} :
    (seqCompose pSpec).Type = Fin.vflatten (fun i => (pSpec i).Type) := by
  rfl

namespace FullTranscript

/-- Sequential composition of a family of `FullTranscript`s, indexed by `i : Fin m`.

Defined for definitional equality, so that the following holds definitionally:
- `seqCompose !h[] = !h[]`
- `seqCompose !h[T₁] = T₁`
- `seqCompose !h[T₁, T₂] = T₁ ++ₜ T₂`
- `seqCompose !h[T₁, T₂, T₃] = T₁ ++ₜ (T₂ ++ₜ T₃)`
- and so on.

TODO: add notation `∑ i, T i` for `seqCompose` -/
@[inline]
def seqCompose {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)}
    (T : ∀ i, FullTranscript (pSpec i)) : FullTranscript (seqCompose pSpec) :=
  Fin.hflatten T

@[simp]
theorem seqCompose_zero {n : Fin 0 → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)}
    {T : ∀ i, FullTranscript (pSpec i)} :
    seqCompose T = !h[] := rfl

@[simp]
theorem seqCompose_succ_eq_append {m : ℕ} {n : Fin (m + 1) → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)}
    {T : ∀ i, FullTranscript (pSpec i)} :
    seqCompose T = append (T 0) (seqCompose (fun i => T (Fin.succ i))) := by
  rfl

@[simp]
theorem seqCompose_embedSum {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)}
    {T : ∀ i, FullTranscript (pSpec i)} (i : Fin m) (j : Fin (n i)) :
    seqCompose T (Fin.embedSum i j) = cast (by simp) (T i j) := by
  simp [seqCompose, cast]; congr 1

end FullTranscript

section Append

variable {ι : Type} {oSpec : OracleSpec ι} {Stmt₁ Wit₁ Stmt₂ Wit₂ Stmt₃ Wit₃ : Type}
  {m n : ℕ} {pSpec₁ : ProtocolSpec m} {pSpec₂ : ProtocolSpec n}

/-- If two protocols have sampleable challenges, then their concatenation also has sampleable
  challenges. -/
@[inline]
instance [h₁ : ∀ i, SampleableType (pSpec₁.Challenge i)]
    [h₂ : ∀ i, SampleableType (pSpec₂.Challenge i)] :
    ∀ i, SampleableType ((pSpec₁ ++ₚ pSpec₂).Challenge i) :=
  fun ⟨i, h⟩ => Fin.fappend₂ (A := Direction) (B := Type)
    (F := fun dir type => (h : dir = .V_to_P) → SampleableType type)
    (α₁ := pSpec₁.dir) (β₁ := pSpec₂.dir)
    (α₂ := pSpec₁.Type) (β₂ := pSpec₂.Type) (fun i h => h₁ ⟨i, h⟩) (fun i h => h₂ ⟨i, h⟩) i h

/-- If two protocols' challenge types are inhabited, then their concatenation's challenge types are
    also inhabited. -/
@[inline]
instance [h₁ : ∀ i, Inhabited (pSpec₁.Challenge i)]
    [h₂ : ∀ i, Inhabited (pSpec₂.Challenge i)] :
    ∀ i, Inhabited ((pSpec₁ ++ₚ pSpec₂).Challenge i) :=
  fun ⟨i, h⟩ => Fin.fappend₂ (A := Direction) (B := Type)
    (F := fun dir type => (h : dir = .V_to_P) → Inhabited type)
    (α₁ := pSpec₁.dir) (β₁ := pSpec₂.dir)
    (α₂ := pSpec₁.Type) (β₂ := pSpec₂.Type) (fun i h => h₁ ⟨i, h⟩) (fun i h => h₂ ⟨i, h⟩) i h

/-- If two protocols' challenge types are finite, then their concatenation's challenge types are
    also finite. -/
@[inline]
instance [h₁ : ∀ i, Fintype (pSpec₁.Challenge i)]
    [h₂ : ∀ i, Fintype (pSpec₂.Challenge i)] :
    ∀ i, Fintype ((pSpec₁ ++ₚ pSpec₂).Challenge i) :=
  fun ⟨i, h⟩ => Fin.fappend₂ (A := Direction) (B := Type)
    (F := fun dir type => (h : dir = .V_to_P) → Fintype type)
    (α₁ := pSpec₁.dir) (β₁ := pSpec₂.dir)
    (α₂ := pSpec₁.Type) (β₂ := pSpec₂.Type) (fun i h => h₁ ⟨i, h⟩) (fun i h => h₂ ⟨i, h⟩) i h

/-- If two protocols' messages have oracle representations, then their concatenation's messages also
    have oracle representations. -/
instance [O₁ : ∀ i, OracleInterface (pSpec₁.Message i)]
    [O₂ : ∀ i, OracleInterface (pSpec₂.Message i)] :
    ∀ i, OracleInterface ((pSpec₁ ++ₚ pSpec₂).Message i) :=
  fun ⟨i, h⟩ => Fin.fappend₂ (A := Direction) (B := Type)
    (F := fun dir type => (h : dir = .P_to_V) → OracleInterface type)
    (α₁ := pSpec₁.dir) (β₁ := pSpec₂.dir)
    (α₂ := pSpec₁.Type) (β₂ := pSpec₂.Type) (fun i h => O₁ ⟨i, h⟩) (fun i h => O₂ ⟨i, h⟩) i h

instance : ∀ i, OracleInterface ((pSpec₁ ++ₚ pSpec₂).Challenge i) := challengeOracleInterface

/-- The challenge type of an appended protocol at a left-injected challenge index agrees with the
challenge type of the left component. This is the transport fact needed to move challenge data
across `++ₚ`; it is `append_Type_castAdd` at the underlying round index, since `ChallengeIdx.inl`
is `Fin.castAdd` on rounds. -/
theorem challenge_append_inl (i : ChallengeIdx pSpec₁) :
    (pSpec₁ ++ₚ pSpec₂).Challenge (ChallengeIdx.inl i) = pSpec₁.Challenge i :=
  append_Type_castAdd (pSpec₁ := pSpec₁) (pSpec₂ := pSpec₂) i.1

/-- The message type of an appended protocol at a left-injected message index agrees with the
message type of the left component. The message-side counterpart of `challenge_append_inl`, and
likewise `append_Type_castAdd` at the underlying round index, since `MessageIdx.inl` is
`Fin.castAdd` on rounds. -/
theorem message_append_inl (i : MessageIdx pSpec₁) :
    (pSpec₁ ++ₚ pSpec₂).Message (MessageIdx.inl i) = pSpec₁.Message i :=
  append_Type_castAdd (pSpec₁ := pSpec₁) (pSpec₂ := pSpec₂) i.1

/-- The message type of an appended protocol at a right-injected message index agrees with the
message type of the right component. Dual to `message_append_inl`. -/
theorem message_append_inr (i : MessageIdx pSpec₂) :
    (pSpec₁ ++ₚ pSpec₂).Message (MessageIdx.inr i) = pSpec₂.Message i :=
  append_Type_natAdd (pSpec₁ := pSpec₁) (pSpec₂ := pSpec₂) i.1

/-- The challenge type of an appended protocol at a right-injected challenge index agrees with the
challenge type of the right component. Dually to `challenge_append_inl`, this is
`append_Type_natAdd` at the underlying round index. -/
theorem challenge_append_inr (i : ChallengeIdx pSpec₂) :
    (pSpec₁ ++ₚ pSpec₂).Challenge (ChallengeIdx.inr i) = pSpec₂.Challenge i :=
  append_Type_natAdd (pSpec₁ := pSpec₁) (pSpec₂ := pSpec₂) i.1

/-- The challenge oracles of the left component embed into those of the appended protocol,
by reindexing along `ChallengeIdx.inl`. -/
instance subSpec_challenge_append_left :
    [pSpec₁.Challenge]ₒ ⊂ₒ [(pSpec₁ ++ₚ pSpec₂).Challenge]ₒ :=
  subSpecOfChallengeReindex ChallengeIdx.inl (challenge_append_inl (pSpec₂ := pSpec₂))

/-- The left inclusion is lawful, so lifting along it preserves the distribution and support of
challenge queries. -/
instance lawfulSubSpec_challenge_append_left :
    [pSpec₁.Challenge]ₒ ˡ⊂ₒ [(pSpec₁ ++ₚ pSpec₂).Challenge]ₒ :=
  lawfulSubSpecOfChallengeReindex ChallengeIdx.inl (challenge_append_inl (pSpec₂ := pSpec₂))

/-- The challenge oracles of the right component embed into those of the appended protocol,
by reindexing along `ChallengeIdx.inr`.

Note that for the degenerate `pSpec ++ₚ pSpec` both this and `subSpec_challenge_append_left` apply,
and typeclass resolution picks this one (declared later), routing challenges into the second copy.
That mirrors VCV-io's `subSpec_add_left` / `subSpec_add_right` for `spec + spec`; pass the intended
instance explicitly if the two components can coincide. -/
instance subSpec_challenge_append_right :
    [pSpec₂.Challenge]ₒ ⊂ₒ [(pSpec₁ ++ₚ pSpec₂).Challenge]ₒ :=
  subSpecOfChallengeReindex ChallengeIdx.inr (challenge_append_inr (pSpec₁ := pSpec₁))

/-- The right inclusion is lawful, so lifting along it preserves the distribution and support of
challenge queries. -/
instance lawfulSubSpec_challenge_append_right :
    [pSpec₂.Challenge]ₒ ˡ⊂ₒ [(pSpec₁ ++ₚ pSpec₂).Challenge]ₒ :=
  lawfulSubSpecOfChallengeReindex ChallengeIdx.inr (challenge_append_inr (pSpec₁ := pSpec₁))

/-- The two inclusions occupy disjoint parts of the appended challenge interface: a left-injected
round index is `< m` and a right-injected one is `≥ m`. This is what rules out the two components'
challenge queries aliasing each other after composition.

Currently unconsumed: recorded because `Verifier.append_soundness` will need it, and because VCV-io
ships the analogue for `spec₁ + spec₂`. -/
instance disjointSubSpec_challenge_append_left_right :
    OracleSpec.DisjointSubSpec
      [pSpec₁.Challenge]ₒ [pSpec₂.Challenge]ₒ [(pSpec₁ ++ₚ pSpec₂).Challenge]ₒ :=
  disjointSubSpecOfChallengeReindex _ (challenge_append_inl (pSpec₂ := pSpec₂))
    _ (challenge_append_inr (pSpec₁ := pSpec₁)) <| by
      intro i i' h
      have hv := congrArg (fun (j : ChallengeIdx (pSpec₁ ++ₚ pSpec₂)) => (j.1 : ℕ)) h
      simp only [ChallengeIdx.inl, ChallengeIdx.inr, Fin.val_castAdd, Fin.val_natAdd] at hv
      have := i.1.isLt
      omega

/-- `disjointSubSpec_challenge_append_left_right` with the two components swapped, matching
VCV-io's pairing of `disjointSubSpec_add_left_right` / `disjointSubSpec_add_right_left`. -/
instance disjointSubSpec_challenge_append_right_left :
    OracleSpec.DisjointSubSpec
      [pSpec₂.Challenge]ₒ [pSpec₁.Challenge]ₒ [(pSpec₁ ++ₚ pSpec₂).Challenge]ₒ where
  disjoint_onQuery t₂ t₁ h :=
    (disjointSubSpec_challenge_append_left_right.disjoint_onQuery t₁ t₂ h.symm)

/-! The two lemmas below are the regression anchors for the inclusions above. Nothing in the
`SubSpec` / `LawfulSubSpec` / `DisjointSubSpec` interface pins down the response transport — any
fibrewise automorphism composed with the cast satisfies all three — so these `rfl`-level
computations are what actually fix the semantics, and what would break if `ChallengeIdx.inl` /
`ChallengeIdx.inr`, `ProtocolSpec.append` or the transport lemmas were changed underneath.
They also give downstream proofs (notably `Prover.append_run`) a rewrite target, in the same spirit
as VCV-io's `liftM_add_left_query` / `liftM_add_right_query`. -/

/-- Lifting a left-component challenge query queries the appended protocol at the left-injected
index and transports the response back along `challenge_append_inl`. -/
@[simp] theorem liftM_challenge_append_inl (i : ChallengeIdx pSpec₁) :
    (liftM (OracleSpec.query (spec := [pSpec₁.Challenge]ₒ) ⟨i, ()⟩) :
        OracleQuery [(pSpec₁ ++ₚ pSpec₂).Challenge]ₒ (pSpec₁.Challenge i))
      = ⟨⟨ChallengeIdx.inl i, ()⟩, cast (challenge_append_inl (pSpec₂ := pSpec₂) i)⟩ := rfl

/-- Lifting a right-component challenge query queries the appended protocol at the right-injected
index and transports the response back along `challenge_append_inr`. -/
@[simp] theorem liftM_challenge_append_inr (i : ChallengeIdx pSpec₂) :
    (liftM (OracleSpec.query (spec := [pSpec₂.Challenge]ₒ) ⟨i, ()⟩) :
        OracleQuery [(pSpec₁ ++ₚ pSpec₂).Challenge]ₒ (pSpec₂.Challenge i))
      = ⟨⟨ChallengeIdx.inr i, ()⟩, cast (challenge_append_inr (pSpec₁ := pSpec₁) i)⟩ := rfl

/-- `getChallenge`-level form of `liftM_challenge_append_inl`: the shape that appears when a
left-component prover's run is lifted into the appended protocol. -/
@[simp] theorem liftM_getChallenge_append_inl (i : ChallengeIdx pSpec₁) :
    (liftM (pSpec₁.getChallenge i) :
        OracleComp [(pSpec₁ ++ₚ pSpec₂).Challenge]ₒ (pSpec₁.Challenge i))
      = cast (challenge_append_inl (pSpec₂ := pSpec₂) i) <$>
          (pSpec₁ ++ₚ pSpec₂).getChallenge (ChallengeIdx.inl i) := rfl

/-- `getChallenge`-level form of `liftM_challenge_append_inr`. -/
@[simp] theorem liftM_getChallenge_append_inr (i : ChallengeIdx pSpec₂) :
    (liftM (pSpec₂.getChallenge i) :
        OracleComp [(pSpec₁ ++ₚ pSpec₂).Challenge]ₒ (pSpec₂.Challenge i))
      = cast (challenge_append_inr (pSpec₁ := pSpec₁) i) <$>
          (pSpec₁ ++ₚ pSpec₂).getChallenge (ChallengeIdx.inr i) := rfl

end Append

section SeqCompose

def sigmaChallengeIdxToSeqCompose {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)}
    (i : Fin m) (j : (pSpec i).ChallengeIdx) : (seqCompose pSpec).ChallengeIdx :=
  ⟨Fin.embedSum i j.1, by simp [j.property]⟩

def seqComposeChallengeIdxToSigma {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)}
    (k : (seqCompose pSpec).ChallengeIdx) : (i : Fin m) × (pSpec i).ChallengeIdx :=
  let ij := Fin.splitSum k.1
  ⟨ij.1, ⟨ij.2, by
    simp [ij]; have := k.property; simp at this
    have hk : k.1 = Fin.embedSum ij.1 ij.2 := by simp [ij]
    simp [hk] at this
    exact this⟩⟩

/-- The challenge type of a sequential composition at a combined challenge index equals the
challenge type of the component protocol at the decoded component challenge index. This is the
transport fact needed whenever per-round challenge data must be moved across `seqCompose`. -/
theorem seqCompose_challenge_eq {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)}
    (i : (seqCompose pSpec).ChallengeIdx) :
    (seqCompose pSpec).Challenge i =
      (pSpec (seqComposeChallengeIdxToSigma i).1).Challenge
        (seqComposeChallengeIdxToSigma i).2 := by
  unfold ProtocolSpec.Challenge seqComposeChallengeIdxToSigma
  simp only [seqCompose_type]
  conv_lhs => rw [← Fin.embedSum_splitSum i.1]
  rw [Fin.vflatten_embedSum]

/-- The equivalence between the challenge indices of the individual protocols and the challenge
    indices of the sequential composition. -/
def seqComposeChallengeEquiv {m : ℕ} {n : Fin m → ℕ} (pSpec : ∀ i, ProtocolSpec (n i)) :
    (i : Fin m) × (pSpec i).ChallengeIdx ≃ (seqCompose pSpec).ChallengeIdx where
  -- TODO: write lemmas about `finSigmaFinEquiv` in mathlib with the one defined via `Fin.dfoldl`
  toFun := fun ⟨i, j⟩ => sigmaChallengeIdxToSeqCompose i j
  invFun := seqComposeChallengeIdxToSigma
  left_inv := by
    intro ⟨i, j⟩
    simp only [seqComposeChallengeIdxToSigma, sigmaChallengeIdxToSeqCompose]
    rw! (castMode := .all) [Fin.splitSum_embedSum i j.1]
    rfl
  right_inv := by intro; simp [seqComposeChallengeIdxToSigma, sigmaChallengeIdxToSeqCompose]

def sigmaMessageIdxToSeqCompose {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)}
    (i : Fin m) (j : (pSpec i).MessageIdx) : (seqCompose pSpec).MessageIdx :=
  ⟨Fin.embedSum i j.1, by simp [j.property]⟩

def seqComposeMessageIdxToSigma {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)}
    (k : (seqCompose pSpec).MessageIdx) : (i : Fin m) × (pSpec i).MessageIdx :=
  let ij := Fin.splitSum k.1
  ⟨ij.1, ⟨ij.2, by
    simp [ij]; have := k.property; simp at this
    have hk : k.1 = Fin.embedSum ij.1 ij.2 := by simp [ij]
    simp [hk] at this
    exact this⟩⟩

/-- The equivalence between the message indices of the individual protocols and the message
    indices of the sequential composition. -/
def seqComposeMessageEquiv {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)} :
    (i : Fin m) × (pSpec i).MessageIdx ≃ (seqCompose pSpec).MessageIdx where
  toFun := fun ⟨i, msgIdx⟩ => sigmaMessageIdxToSeqCompose i msgIdx
  invFun := seqComposeMessageIdxToSigma
  left_inv := by
    intro ⟨i, j⟩
    simp only [seqComposeMessageIdxToSigma, sigmaMessageIdxToSeqCompose]
    rw! (castMode := .all) [Fin.splitSum_embedSum i j.1]
    rfl
  right_inv := by intro; simp [seqComposeMessageIdxToSigma, sigmaMessageIdxToSeqCompose]

instance {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)}
    [inst : ∀ i, ∀ j, SampleableType ((pSpec i).Challenge j)] :
    ∀ k, SampleableType ((seqCompose pSpec).Challenge k) :=
  fun ⟨k, h⟩ => Fin.fflatten₂
    (A := Direction) (B := Type) (F := fun dir type => (h : dir = .V_to_P) → SampleableType type)
    (fun i' j' h' => inst i' ⟨j', h'⟩) k h

instance {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)}
    [inst : ∀ i, ∀ j, Inhabited ((pSpec i).Challenge j)] :
    ∀ k, Inhabited ((seqCompose pSpec).Challenge k) :=
  fun ⟨k, h⟩ => Fin.fflatten₂
    (A := Direction) (B := Type) (F := fun dir type => (h : dir = .V_to_P) → Inhabited type)
    (fun i' j' h' => inst i' ⟨j', h'⟩) k h

instance {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)}
    [inst : ∀ i, ∀ j, Fintype ((pSpec i).Challenge j)] :
    ∀ k, Fintype ((seqCompose pSpec).Challenge k) :=
  fun ⟨k, h⟩ => Fin.fflatten₂
    (A := Direction) (B := Type) (F := fun dir type => (h : dir = .V_to_P) → Fintype type)
    (fun i' j' h' => inst i' ⟨j', h'⟩) k h

/-- If all protocols' messages have oracle interfaces, then the messages of their sequential
  composition also have oracle interfaces. -/
instance {m : ℕ} {n : Fin m → ℕ} {pSpec : ∀ i, ProtocolSpec (n i)}
    [Oₘ : ∀ i, ∀ j, OracleInterface ((pSpec i).Message j)] :
    ∀ k, OracleInterface ((seqCompose pSpec).Message k) :=
  fun ⟨k, h⟩ => Fin.fflatten₂
    (A := Direction) (B := Type) (F := fun dir type => (h : dir = .P_to_V) → OracleInterface type)
    (fun i' j' h' => Oₘ i' ⟨j', h'⟩) k h

end SeqCompose

end ProtocolSpec
